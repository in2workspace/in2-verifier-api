plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id 'checkstyle'
	id 'org.owasp.dependencycheck' version '9.1.0'
	id 'io.swagger.core.v3.swagger-gradle-plugin' version '2.2.22'
	id 'org.sonarqube' version '5.1.0.4882'
}

group = 'es.in2'
version = '1.3.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		sourceCompatibility = JavaLanguageVersion.of(17)
		targetCompatibility = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

checkstyle {
	toolVersion = '10.16.0'
	configFile = file("${rootDir}/checkstyle/checkstyle.xml")
}

checkstyleMain {
	source = 'src/main/java'
}

checkstyleTest {
	source = 'src/test/java'
}

sonar {
	properties {
		property "sonar.projectName", "IN2 Verifier API"
		property "sonar.projectKey", "in2workspace_in2-verifier-api"
		property "sonar.organization", "in2workspace"
		property "sonar.host.url", "https://sonarcloud.io"
		property 'sonar.coverage.exclusions',
				"src/main/java/es/in2/vcverifier/VcVerifierApplication.java, " +
						"src/main/java/es/in2/vcverifier/service/impl/ClientAssertionValidationServiceImpl.java, " +
						"src/main/java/es/in2/vcverifier/service/impl/TrustFrameworkServiceImpl.java, " +
						"src/main/java/es/in2/vcverifier/service/impl/CertificateValidationServiceImpl.java, " +
						"src/main/java/es/in2/verifier/security/filters/CustomTokenRequestConverter.java, " +
						"src/main/java/es/in2/verifier/security/AuthorizationServerConfig.java, " +
						"src/main/java/es/in2/vcverifier/exception/MismatchOrganizationIdentifierException.java"
	}
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
}

dependencies {
	// Starters
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// Cryptography
	implementation 'com.nimbusds:nimbus-jose-jwt:9.40'
	implementation 'org.bitcoinj:bitcoinj-core:0.17-alpha5'
	implementation 'io.github.novacrypto:Base58:2022.01.17'
	//Jackson
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2'
	// JSON
	implementation 'org.json:json:20230227'
	// QR GENERATOR
	implementation 'com.github.kenglxn.QRGen:javase:3.0.1'
	// DevTools
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// Websocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('compileJava') {
	inputs.files(tasks.named('processResources'))
}

tasks.register('printProjectName') {
	doLast {
		println rootProject.name
	}
}

tasks.register('printVersion') {
	doLast {
		println version
	}
}

tasks.register('checkstyle') {
	reports {
		xml.required = false
		html.required = true
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
	dependsOn(tasks.test)
	reports {
		xml.required.set(true)
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}
	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: [
				"**/TrustFrameworkServiceImpl.class",
				"**/CertificateValidationServiceImpl.class",
				"**/MismatchOrganizationIdentifierException.class",
				"**/CustomTokenRequestConverter.class",
				"**/AuthorizationServerConfig.class",
		])
	}))
}
